buildscript {
    ext {
        springBootVersion = '2.4.2'
        springRetryVersion = '1.3.0'
        kotlinVersion = '1.4.10'
        lombokVersion = '1.18.12'
        okhttp3Version = '4.9.0'
        powermockVersion = '2.0.7'
    }
    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
        classpath 'io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE'
        classpath 'io.spring.gradle:propdeps-plugin:0.0.9.RELEASE'
        classpath 'gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.11'
        classpath 'net.rdrei.android.buildtimetracker:gradle-plugin:0.11.0'
    }
}


apply plugin: 'java'

configure(subprojects.findAll { relativePath(it.projectDir).startsWith('app') }) {
    apply plugin: 'java'
}

configure(subprojects.findAll { relativePath(it.projectDir).startsWith('libs/') }) {
    apply plugin: 'java-library'
}


allprojects {
    apply plugin: 'idea'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'io.spring.dependency-management'

    group 'moe.pine.mapbot'

    repositories {
        mavenCentral()
    }

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    compileJava {
        options.encoding = 'UTF-8'
        options.compilerArgs << '-Xlint:unchecked'
        options.compilerArgs << '-Xlint:deprecation'
    }

    test {
        useJUnitPlatform()
    }

    configurations {
        implementation.exclude module: 'spring-boot-starter-tomcat'
    }
    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.jetbrains.kotlin:kotlin-bom:$kotlinVersion"

        }
        dependencies {
            dependency 'javax.servlet:javax.servlet-api:4.0.1'
            dependency "org.springframework.retry:spring-retry:$springRetryVersion"

            // Guava
            dependency 'com.google.guava:guava:27.0.1-jre'

            // Apache Commons
            dependency 'org.apache.commons:commons-lang3:3.8.1'
            dependency 'org.apache.commons:commons-collections4:4.3'

            // Slack
            dependency 'com.github.seratch:jslack:3.0.1'
            dependency 'javax.websocket:javax.websocket-api:1.1'

            // Utilities
            dependency 'com.github.maricn:logback-slack-appender:1.4.0'
            dependency "org.projectlombok:lombok:$lombokVersion"
            dependency 'org.jsoup:jsoup:1.12.1'

            // My utilities
            dependency 'moe.pine:heroku-addons:0.3.3'
            dependency 'moe.pine:spring-cache-interceptors:0.2.0'

            // Test
            dependency "org.powermock:powermock-reflect:$powermockVersion"
            dependency "org.powermock:powermock-api-mockito2:$powermockVersion"
            dependency "org.powermock:powermock-module-junit4:$powermockVersion"
            dependency "com.squareup.okhttp3:okhttp:$okhttp3Version"
            dependency "com.squareup.okhttp3:mockwebserver:$okhttp3Version"
        }
    }
    dependencies {
        // Lombok
        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'
        testAnnotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'

        // Annotations
        compileOnly 'net.jcip:jcip-annotations:1.0'
        compileOnly 'com.github.spotbugs:spotbugs-annotations:3.1.3'
    }


    // ----- JaCoCo -----------------------------------

    apply plugin: 'jacoco'

    jacoco {
        toolVersion = '0.8.3'
    }
}


// ----- JaCoCo -------------------------------------------

task jacocoMerge(type: JacocoMerge) {
    dependsOn(subprojects.jacocoTestReport.dependsOn)
    mustRunAfter(subprojects.jacocoTestReport.mustRunAfter)
    destinationFile = file("${buildDir}/jacoco/test.exec")
    executionData = files(subprojects.jacocoTestReport.executionData)
            .filter { f -> f.exists() }
}

jacocoTestReport {
    dependsOn(jacocoMerge)
    reports {
        xml.enabled = true
        html.enabled = true
    }

    additionalSourceDirs.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    sourceDirectories.setFrom files(subprojects.sourceSets.main.allSource.srcDirs)
    classDirectories.setFrom files(subprojects.sourceSets.main.output)
}


// ----- Configuration ------------------------------------

def confFiles = fileTree('credentials').include('**/*.json', '**/*.xml', '**/*.yml')
confFiles.forEach { File src ->
    def dest = new File(
            src.absolutePath.replace("${rootDir.absolutePath}/credentials", rootDir.absolutePath))
    if (src.name.endsWith('.yml')) {
        dest.write("# This YAML source file was generated by the Gradle.\n\n")
    } else {
        dest.setBytes([] as byte[])
    }
    dest.append(src.readBytes())
}
